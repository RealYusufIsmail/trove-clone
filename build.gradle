import static java.util.Objects.nonNull
import static java.util.stream.Collectors.toList

///////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2009-2010, Rob Eden All Rights Reserved.
//
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
//
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU Lesser General Public
// License along with this program; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
///////////////////////////////////////////////////////////////////////////////

apply plugin: 'base'
group = "net.sf.trove4j"
version = "3.1.0"

subprojects {
    String pomName = "Trove (${project.name})"

    apply plugin: 'java'
    apply plugin: 'osgi'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    project.group = rootProject.group
    project.version = rootProject.version

    repositories {
        mavenCentral()
    }

    configurations {
        generateTemplates
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.11'
        generateTemplates project(":generator")
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'Trove',
                    'Implementation-Version': "${version}",
                    'Implementation-URL': 'https://bitbucket.org/robeden/trove/',
                    'Sealed': 'false'

            // OSGI stuff
            instruction 'Bundle-Name', 'Trove'
            instruction 'Bundle-SymbolicName', 'gnu.trove'
            instruction 'Bundle-Version', "${version}"
            instruction 'Bundle-Vendor', 'Trove Development Team'
            instruction 'Bundle-License', 'http://www.gnu.org/licenses/lgpl-2.1.html'
            instruction 'Bundle-ContactAddress:', 'http://trove.starlight-systems.com/contacts'
        }
    }

    task generateTemplates( type: JavaExec, dependsOn: ":generator:build" ) {
        inputs.dir('src/main/templates')
        outputs.dir("${buildDir}/generated-src")

        classpath configurations.generateTemplates
        main = 'gnu.trove.generator.Generator'
        args = [ 'src/main/templates', "${buildDir}/generated-src" ]
        sourceSets.main.java.srcDirs += "${buildDir}/generated-src"
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        classifier = "sources"
        sourceSets.each { sourceSet ->
            from sourceSet.allJava
        }
    }

    task docJar(type: Jar, dependsOn: javadoc) {
        classifier = "javadoc"
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourceJar
        archives docJar
    }

    publishing {
        publications {
            "${project.name}"(MavenPublication) {
                artifactId = project.archivesBaseName
                from components.java
                artifact sourceJar
                artifact docJar
                pom {
                    name = pomName
                    description = 'High performance collections for Java'
                    url = "https://bitbucket.org/trove4j/trove/src/master/"
                    licenses {
                        license {
                            name = "GNU Lesser General Public License 2.1"
                            url = "http://www.gnu.org/licenses/lgpl-2.1.txt"
                            distribution = "repo"
                        }
                    }
                    scm {
                        url = "https://bitbucket.org/trove4j/trove/src/master/"
                    }
                    developers {
                        developer {
                            name = "Rob Eden"
                            email = "robeden1@gmail.com"
                        }
                        developer {
                            name = "Johan Parent"
                            email = "jparent@users.sourceforge.net"
                        }
                        developer {
                            name = "Jeff Randall"
                            email = "randall@uph.com"
                        }
                        developer {
                            name = "Eric D. Friedman"
                            email = "ericdf@users.sourceforge.net"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url = "$buildDir/release"
            }
        }
    }
}

project(":core") {
    compileJava.dependsOn(generateTemplates)
    archivesBaseName = "trove4j"
    description = "The Trove library provides high speed regular and primitive collections for Java."
}

project(":experimental") {
    compileJava.dependsOn(generateTemplates)
    archivesBaseName = "trove4j-experimental"
    description = "The trove4j experimental library."

    dependencies {
        compile project( ':core' )
    }
}

project(":generator") {
    publishGeneratorPublicationToMavenRepository.onlyIf {false}
    publishGeneratorPublicationToMavenLocal.onlyIf{false}
    publish.onlyIf {false}
}
